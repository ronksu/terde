define [
  'jquery'
  'knockout'
  'lodash'
],(
  $
  ko
  _
) ->

  kelvin2Celcius = (kelvins) ->
    kelvins - 273.15

  class openWeatherHelsinki
    constructor: () ->
      @temperature = ko.observable()
      @temperatureFormatted = ko.computed =>
        "#{Math.round(kelvin2Celcius(@temperature()))} Â°C"

      @cloudinessInPercent = ko.observable()
      @logicalCloudiness = ko.computed =>
        switch
          when @cloudinessInPercent() < 30
            'clear'
          when @cloudinessInPercent() < 70
            'partlycloudy'
          else
            'cloudy'

    init: () ->
      # @TODO check cookie first
      weatherDataRequest = $.ajax
        type: 'GET'
        url: 'http://api.openweathermap.org/data/2.5/weather?id=658225'
        contentType: "application/json"
        dataType: 'jsonp'

      weatherDataRequest.done (data) =>
        if _.isFinite(data?.clouds?.all)
          @cloudinessInPercent(data.clouds.all)

        if _.isFinite(data?.main?.temp)
          @temperature(data.main.temp)
